###############################################################################
# Sudo management API
# brought to you by LastOps (http://lastops.com)
###############################################################################

body file control
# @ignore
{
       namespace => "lastops";
}

bundle agent sudo_enforce_includes
# @brief Changes sudoers file to use explicit inludes only
{
    files:
        "/etc/sudoers"
            create => "true",
            perms => default:mog("0440","root","root"),
            classes => default:if_repaired("created_sudoers");
        "/etc/sudoers"
            edit_line => default:delete_lines_matching("^(?!#include).*"),
            classes => default:if_repaired("cleaned_up_sudoers");
        "/etc/sudoers"
            edit_line => default:delete_lines_matching(".*includedir.*"),
            classes => default:if_repaired("deleted_includedir");

    reports:
        created_sudoers::
            "$(sys.fqhost): /etc/sudoers exists and has proper permissions";
        cleaned_up_sudoers::
            "$(sys.fqhost): Enforcing includes-only /etc/sudoers";
        deleted_includedir::
            "$(sys.fqhost): Enforcing file-based includes in /etc/sudoers";
}

bundle agent sudo_include_json(json, include, template)
# @brief Generates files and include statements for sudoers file
# @param json string Path to a JSON-formatted file describing users
# @param include string Include file name
# @param template string Path to a a template file to use to generate include file
{
        vars:
            "sudo_include_dir" string => "/etc/sudoers.d";

        files:
            "$(sudo_include_dir)/."
                create => "true",
                perms => default:mog("0440","root","root");
            "$(sudo_include_dir)/$(include)"
                create => "true",
                edit_template => "$(template)",
                template_method => "mustache",
                template_data => readjson("$(json)", 4000),
                classes => default:if_repaired("updated_include");
            "/etc/sudoers"
                create => "true",
                edit_line => default:insert_lines("#include $(sudo_include_dir)/$(include)"),
                perms => default:mog("0440","root","root"),
                classes => default:if_repaired("enabled_include");

        reports:
            updated_include::
                "$(sys.fqhost): Updated include $(sudo_include_dir)/$(include)";
            enabled_include::
                "$(sys.fqhost): Enabled include $(sudo_include_dir)/$(include) in /etc/sudoers";
}
