body file control {
       namespace => "lastops";
    }

bundle agent user(user,uid,description,home_dir,group_primary,groups_secondary,shell,pw){
        vars:
            "g_s" slist => splitstring("$(groups_secondary)", ",", 1000);
        files:
            "$(home_dir)/."
                create => "true",
                perms => default:og("$(user)","$(user)");
        users:
            "$(user)"
                policy => "present",
                uid =>  "$(uid)",
                description => "$(description)",
                home_dir => "$(home_dir)",
                group_primary => "$(group_primary)",
                groups_secondary => { @(g_s) },
                shell => "$(shell)",
                password => default:hashed_password("$(pw)");
    }

bundle agent soft_user(user,uid,description,home_dir,group_primary,groups_secondary,shell,pw){
        vars:
            "g_s" slist => splitstring("$(groups_secondary)", ",", 1000);
        classes:
            "add_$(user)" not   => userexists("$(user)");
        files:
            "$(home_dir)/."
                create => "true",
                perms => default:og("$(user)","$(user)"),
                ifvarclass => "add_$(user)";
        users:
            "$(user)"
                policy => "present",
                uid =>  "$(uid)",
                description => "$(description)",
                home_dir => "$(home_dir)",
                group_primary => "$(group_primary)",
                groups_secondary => { @(g_s) },
                shell => "$(shell)",
                password => default:hashed_password("$(pw)"),
                ifvarclass => "add_$(user)";
    }

bundle agent no_user(user){
    users:
        "$(user)"
            policy => "absent";
    }

bundle agent no_user_tidy(user,home_dir){
    users:
        "$(user)"
            policy => "absent";
    files:
       "$(home_dir)"
            delete => default:tidy;
    }

bundle agent lock_user(user){
    users:
        "$(user)"
            policy => "locked";
    }

bundle agent enforce_password(user,pw){
    files:
        "/etc/shadow"
            edit_line => default:set_user_field("$(user)", 2, "$(pw)");
    }

bundle agent enfoce_shell(user,shell){
    files:
        "/etc/passwd"
            edit_line => default:set_user_field("$(user)", 7, "$(shell)");
    }

# bundle agent authorized_keys(user,home_dir,keys){
#     files:
#         "$(home_dir)/.ssh/authorized_keys"
#             edit_line => default:set_user_field("$(user)", 7, "$(shell)"),
#             perms => default:og("$(user)","$(user)");
#     }

# bundle agent enfoce_authorized_keys(user,home_dir,keys){
#     files:
#         "$(home_dir)/.ssh/authorized_keys"
#             edit_line => default:set_user_field("$(user)", 7, "$(shell)");
#     }
